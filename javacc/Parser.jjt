options {
    LOOKAHEAD =  1;
    MULTI = true;
}

PARSER_BEGIN(Parser)

public class Parser {

    int nErrors = 3;

    public static void main(String[] args) throws ParseException {
            Parser parser = new Parser(System.in);
            parser.Program();
    }
}
PARSER_END(Parser)

SKIP :
{
    " " | "\t" | "\r" | "\n"
}

SKIP :
{
  < "//" (~["\r", "\n"])* >
| "/*" : ML_COMMENT_STATE
}

<ML_COMMENT_STATE> SKIP :
{
  "*/" : DEFAULT
| < ~[] >
}

TOKEN:
{
    < INTEGER: (["0"-"9"])+ >
    | <IMPORT: "import">
    | <CLASS: "class">
    | <EXTENDS: "extends">
    | <PUBLIC: "public">
    | <STATIC: "static">
    | <VOID: "void">
    | <MAIN: "main">
    | <STRING: "String">
    | <RETURN: "return">
    | <IF: "if">
    | <ELSE: "else">
    | <WHILE: "while">
    | <TRUE: "true">
    | <FALSE: "false">
    | <THIS: "this">
    | <NEW: "new">
    | <LENGTH: "length">
    | <INT: "int">
    | <BOOLEAN: "boolean">
    | <IDENTIFIER: ["A"-"Z", "a"-"z"](["0"-"9", "A"-"Z", "a" - "z", "_"])* >
    | <LFTSQRBRCKT : "[">
    | <RGHTSQRBRCKT : "]">
    | <RGHTBRCKT : ")">
    | <LFTBRCKT : "(">
    | <LFTCRLBRCKT : "{">
    | <RGHTCRLBRCKT : "}">
    | <SEMICOLON : ";">
    | <DOT : ".">
    | <COMMA : ",">
    | <EXCLAMATION: "!">
    | <EQUALS : "=">
}

SimpleNode Program() : {}
{
    ImportDeclaration() ClassDeclaration() <EOF> { return jjtThis; }
}


void ImportDeclaration() #void : {}
{
    (<IMPORT> ImportStatement() <SEMICOLON> )*
}

void ImportStatement() #void : {  }
{
    ( Identifier()  (<DOT> Identifier() )* <LFTBRCKT>  Type() <RGHTBRCKT> ) #Import
}

void Identifier() #void : {Token t;}
{
    t = <IDENTIFIER> {jjtThis.identifierName = t.image;} #Identifier

}

void ClassDeclaration() #void : {}
{
    <CLASS> <IDENTIFIER>  [<EXTENDS> <IDENTIFIER> ]  <LFTCRLBRCKT> (VarDeclaration())* (MethodDeclaration())* <RGHTCRLBRCKT>
}

void VarDeclaration() #void : {}
{
   Type() <IDENTIFIER> <SEMICOLON>
}

void MethodDeclaration() #void : {}
{
    <PUBLIC>
    (Type() <IDENTIFIER> <LFTBRCKT> [Type() <IDENTIFIER> (<COMMA> Type() <IDENTIFIER>)*] <RGHTBRCKT> <LFTCRLBRCKT> ( LOOKAHEAD(2) VarDeclaration())* (Statement())* <RETURN> Expression() <SEMICOLON> <RGHTCRLBRCKT>
    | (<STATIC> <VOID> <MAIN> <LFTBRCKT> <STRING> (<LFTSQRBRCKT> <RGHTSQRBRCKT> | "[]") <IDENTIFIER> <RGHTBRCKT> <LFTCRLBRCKT> (LOOKAHEAD(2) VarDeclaration())* (Statement())* <RGHTCRLBRCKT> )) //TODO: check if newly added production is correct

}

void Statement() #void : {}
{
    <LFTCRLBRCKT> (Statement())* <RGHTCRLBRCKT>
    | <IF> <LFTBRCKT> Expression() <RGHTBRCKT> Statement() <ELSE> Statement()
    | WhileStatement()
    | NonIdentifierExpression() <SEMICOLON>
    | IdentifierStatement() <SEMICOLON>
}

void IdentifierStatement() #void : {}
{
    <IDENTIFIER>
    (
        <EQUALS> Expression()
        | <LFTSQRBRCKT> Expression() <RGHTSQRBRCKT>
            (<EQUALS> Expression() | ComplementaryExpression() )
        | FragmentExpression()
    )
}

void ComplementaryExpression() #void :{} {
    ArrayAccessExpression()
    |
    FragmentExpression()
}
void TerminalComplementaryExpression() #void :{} {
    TerminalArrayAccessExpression()
    |
    TerminalFragmentExpression()
}

void ArrayAccessExpression() #void : {}{
    <LFTSQRBRCKT> Expression() <RGHTSQRBRCKT> #ArrayAccess ComplementaryExpression()
}

void TerminalArrayAccessExpression() #void : {}{
    <LFTSQRBRCKT> Expression() <RGHTSQRBRCKT> #ArrayAccess
}

void WhileStatement() #void : {}
{
    <WHILE>
    (<LFTBRCKT> | recoverFromError(new int[]{INTEGER,
                                      TRUE,
                                      FALSE,
                                      THIS,
                                      NEW,
                                      EXCLAMATION,
                                      LFTBRCKT,
                                      IDENTIFIER
                                      }, "Got '" + getToken(1) + "', expected '('", false)) // all possible tokens to start an expression

    try {
        Expression()
    }
    catch (ParseException e) {
        recoverFromError(new int[]{RGHTBRCKT}, "Invalid Expression '" + e.currentToken + "'", false); // invalid expression
    }

    (<RGHTBRCKT> | recoverFromError(new int[]{LFTCRLBRCKT,
                                      IF,
                                      WHILE,
                                      INTEGER,
                                      TRUE,
                                      FALSE,
                                      THIS,
                                      NEW,
                                      EXCLAMATION,
                                      LFTBRCKT,
                                      IDENTIFIER
                                      }, "Got '" + getToken(1) + "', expected ')'", false)) // all possible tokens to start a statement

    try {
        Statement()
    }
    catch (ParseException e) {
        recoverFromError(new int[]{RGHTCRLBRCKT}, "Invalid Statement '" + e.currentToken + "'", true); // invalid statement
    }
}

JAVACODE
void recoverFromError(int[] skipTo, String errorMsg, boolean advanceWhenFound) {

    if(nErrors <= 0) System.exit(0);
    nErrors--;

    System.out.println(errorMsg);

    Token t = getToken(1);
    String skipped = "";
    boolean found = false;
    while(!found) {
        for (int i = 0; i < skipTo.length; i++) {
            if (skipTo[i] == getToken(1).kind) {
                found = true;
                break;
            }
        }
        if(!found) {
            skipped += getToken(1).toString();
            t = getNextToken();
       }
    }

    if (advanceWhenFound) {
        skipped += getToken(1).toString();
        getNextToken();
    }

    System.out.println("Skipped '" + skipped + "'\n");
}

void TwoAryExpressions() #void : {} {
     (("&&" Expression() #And(2))
     |("<" TerminalExpression() #LessThan(2))
     | ("+" Expression() #Sum(2))
     | ("-" Expression() #Minus(2))
     | ("*" TerminalExpression() #Mul(2))
     | ("/" TerminalExpression() #Div(2))) ComplementaryExpression()
}

void FragmentExpression() #void : { Token t; }
{
    TwoAryExpressions()
    |<DOT>
        (
        <LENGTH> #Length ComplementaryExpression()
        |
        ((t=<IDENTIFIER> { jjtThis.methodIdentifier = t.image; } <LFTBRCKT> [ Expression() (<COMMA> Expression() )* ] <RGHTBRCKT>)  #MethodCall(2) ComplementaryExpression())
        )
    | {}
}

void TerminalFragmentExpression() #void : { Token t; }
{
    <DOT>
        (
        <LENGTH> #Length ComplementaryExpression()
        |
        ((t=<IDENTIFIER> { jjtThis.methodIdentifier = t.image; } <LFTBRCKT> [ Expression() (<COMMA> Expression() )* ] <RGHTBRCKT>)  #MethodCall(2) ComplementaryExpression())
        )
    | {}
}

void Expression() #void : {}{
    IdentifierExpression()
    |
    NonIdentifierExpression()
}

void NonIdentifierExpression() #void :{ Token t; }{
    t=<INTEGER>{ jjtThis.number = Integer.parseInt(t.image); } #Numeric ComplementaryExpression()
    | <TRUE> { jjtThis.number = 1; } #Numeric ComplementaryExpression()
    | <FALSE> { jjtThis.number = 0; } #Numeric ComplementaryExpression()
    | <THIS>  #This ComplementaryExpression()
    | (
        <NEW>
        t= <IDENTIFIER> "()" { jjtThis.classIdentifier = t.image; } #New ComplementaryExpression()
        | <INT> <LFTSQRBRCKT> Expression() <RGHTSQRBRCKT> #NewArray
        ) ComplementaryExpression()
    | <EXCLAMATION> Expression() #Negation ComplementaryExpression()
    | <LFTBRCKT> Expression() <RGHTBRCKT> ComplementaryExpression()
}

void TerminalExpression() #void :{ Token t; }{
    t=<INTEGER>{ jjtThis.number = Integer.parseInt(t.image); } #Numeric TerminalComplementaryExpression()
    | <TRUE> { jjtThis.number = 1; } #Numeric TerminalComplementaryExpression()
    | <FALSE> { jjtThis.number = 0; } #Numeric TerminalComplementaryExpression()
    | <THIS>  #This TerminalComplementaryExpression()
    | (
        <NEW>
        t= <IDENTIFIER> "()" { jjtThis.classIdentifier = t.image; } #New TerminalComplementaryExpression()
        | <INT> <LFTSQRBRCKT> Expression() <RGHTSQRBRCKT> #NewArray
        ) TerminalComplementaryExpression()
    | "!" Expression() #Negation TerminalComplementaryExpression()
    | <LFTBRCKT> Expression() <RGHTBRCKT> TerminalComplementaryExpression()
}

void IdentifierExpression() #void : {}{
    Identifier() ComplementaryExpression()
}
void Type() #void : {}
{
    (<INT>("[]" #IntArray|{}#Int)) | <BOOLEAN> #Boolean | Identifier()
}