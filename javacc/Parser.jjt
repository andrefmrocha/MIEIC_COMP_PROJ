options {
    LOOKAHEAD =  1;
    MULTI = true;
}

PARSER_BEGIN(Parser)

public class Parser {

    public static void main(String[] args) throws ParseException {
            Parser parser = new Parser(System.in);
            parser.Program();
    }
}
PARSER_END(Parser)

SKIP :
{
    " " | "\t" | "\r" | "\n"
}

SKIP :
{
  < "//" (~["\r", "\n"])* >
| "/*" : ML_COMMENT_STATE
}

<ML_COMMENT_STATE> SKIP :
{
  "*/" : DEFAULT
| < ~[] >
}

TOKEN:
{
    < INTEGER: (["0"-"9"])+ >
    | <IMPORT: "import">
    | <CLASS: "class">
    | <EXTENDS: "extends">
    | <PUBLIC: "public">
    | <STATIC: "static">
    | <VOID: "void">
    | <MAIN: "main">
    | <STRING: "String">
    | <RETURN: "return">
    | <IF: "if">
    | <ELSE: "else">
    | <WHILE: "while">
    | <TRUE: "true">
    | <FALSE: "false">
    | <THIS: "this">
    | <NEW: "new">
    | <LENGTH: "length">
    | <INT: "int">
    | <BOOLEAN: "boolean">
    | <IDENTIFIER: ["A"-"Z", "a"-"z"](["0"-"9", "A"-"Z", "a" - "z", "_"])* >
    | <LFTSQRBRCKT : "[">
    | <RGHTSQRBRCKT : "]">
    | <RGHTBRCKT : ")">
    | <LFTBRCKT : "(">
    | <LFTCRLBRCKT : "{">
    | <RGHTCRLBRCKT : "}">
    | <SEMICOLON : ";">
    | <DOT : ".">
    | <COMMA : ",">
    | <EQUALS : "=">
}

SimpleNode Program() : {}
{
    ImportDeclaration() ClassDeclaration() <EOF> { return jjtThis; }
}


void ImportDeclaration() #void : {}
{
    (<IMPORT> ImportStatement() <SEMICOLON> )*
}

void ImportStatement() #void : {  }
{
    ( Identifier()  (<DOT> Identifier() )* <LFTBRCKT>  Type() <RGHTBRCKT> ) #Import
}

void Identifier() #void : {Token t;}
{
    t = <IDENTIFIER> {jjtThis.identifierName = t.image;} #Identifier

}

void ClassDeclaration() #void : {}
{
    (<CLASS> Identifier()   [<EXTENDS> Identifier() #Extend ]  <LFTCRLBRCKT> (VarDeclaration())* (MethodDeclaration())* <RGHTCRLBRCKT>) #Class
}

void DeclarationFragment() #void : {}
{
   Identifier() <SEMICOLON> #Var(2)
}


void VarDeclaration() #void : {}
{
   Type() DeclarationFragment()
}

void NonTypeVarDeclaration() #void : {}
{
   NonIdentifierType() DeclarationFragment()
}

void MethodBody() #void : {} {

    NonTypeVarDeclaration() MethodBody()
    |
    Identifier() ( DeclarationFragment() MethodBody() | IdentifierStatementFragment() ( Statement() )* )
    |
    NonIdentifierStatement() (Statement())*
    |
    {}
}

void MethodDeclaration() #void : {}
{
    (<PUBLIC>
    (MethodName() <LFTBRCKT> [Parameter() (<COMMA> Parameter())*] <RGHTBRCKT>  <LFTCRLBRCKT> (MethodBody() <RETURN> Expression() <SEMICOLON>)#MethodBody <RGHTCRLBRCKT>
    | ((<STATIC> <VOID> <MAIN> <LFTBRCKT> <STRING> (<LFTSQRBRCKT> <RGHTSQRBRCKT> | "[]") Identifier() <RGHTBRCKT> ) #Main <LFTCRLBRCKT> MethodBody() #MethodBody <RGHTCRLBRCKT>  ))
    )#Method //TODO: check if newly added production is correct

}


void MethodName() #void : {}
{
    (Type() Identifier()) #MethodName
}


void Parameter() #void : {}
{
    (Type() Identifier()) #Parameter
}


void NonIdentifierStatement() #void : {}
{
    <LFTCRLBRCKT> (Statement())* <RGHTCRLBRCKT>
    | <IF> <LFTBRCKT> Expression() <RGHTBRCKT> Statement() <ELSE> Statement()
    | <WHILE> <LFTBRCKT> Expression() <RGHTBRCKT> Statement()
    | NonIdentifierExpression() <SEMICOLON>
}

void Statement() #void : {}
{
     NonIdentifierStatement()
    |
     IdentifierStatement()
}

void IdentifierStatement() #void : {}
{
    <IDENTIFIER> IdentifierStatementFragment()
}

void IdentifierStatementFragment() #void : {} {
    (<EQUALS> Expression()
    | <LFTSQRBRCKT> Expression() <RGHTSQRBRCKT>
        (<EQUALS> Expression() | ComplementaryExpression() )
    | FragmentExpression() ) <SEMICOLON>
}

void ComplementaryExpression() #void :{} {
    ArrayAccessExpression()
    |
    FragmentExpression()
}

void ArrayAccessExpression() #void : {}{
    <LFTSQRBRCKT> Expression() <RGHTSQRBRCKT> #ArrayAccess ComplementaryExpression()
}

void TwoAryExpressions() #void : {} {
     (("&&" Expression() #And(2))
     |("<" Expression() #LessThan(2))
     | ("+" Expression() #Sum(2))
     | ("-" Expression() #Minus(2))
     | ("*" Expression() #Mul(2))
     | ("/" Expression() #Div(2))) ComplementaryExpression()
}

void FragmentExpression() #void : { Token t; }
{
    TwoAryExpressions()
    |<DOT> ( <LENGTH> #Length
    | (t=<IDENTIFIER> { jjtThis.methodIdentifier = t.image; } <LFTBRCKT> [ Expression() (<COMMA> Expression() )* ] <RGHTBRCKT>) #MethodCall )  ComplementaryExpression()
    | {}
}

void Expression() #void : {}{
    IdentifierExpression()
    |
    NonIdentifierExpression()
}

void NonIdentifierExpression() #void :{ Token t; }{
    t=<INTEGER>{ jjtThis.number = Integer.parseInt(t.image); } #Numeric ComplementaryExpression()
    | <TRUE> { jjtThis.number = 1; } #Numeric ComplementaryExpression()
    | <FALSE> { jjtThis.number = 0; } #Numeric ComplementaryExpression()
    | <THIS>  ComplementaryExpression() #This
    | <NEW> (t= <IDENTIFIER> "()" { jjtThis.classIdentifier = t.image; } #New |
     (<INT> <LFTSQRBRCKT> Expression() <RGHTSQRBRCKT>) #NewArray ) ComplementaryExpression()
    | "!" Expression() #Negation ComplementaryExpression()
    | <LFTBRCKT> Expression() <RGHTBRCKT> ComplementaryExpression()
}

void IdentifierExpression() #void : { Token t; }{
    t=<IDENTIFIER> { jjtThis.identifierName = t.image; }#Identifier ComplementaryExpression()
}

void Type() #void : {} //TODO: Type no longer a Token
{
    NonIdentifierType() | Identifier()
}

void NonIdentifierType() #void : {}
{
       (<INT>("[]" #IntArray|{}#Int))  | <BOOLEAN> #Boolean | <VOID> #Void
}